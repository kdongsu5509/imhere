plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.2.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.2.21'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.jetbrains.kotlin.plugin.jpa' version '2.2.21'
    id 'jacoco'
}

group = 'com.kdongsu5509'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    //WEB
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    //SPRING SECURITY
    implementation 'org.springframework.boot:spring-boot-starter-security'
    //SPRING DATA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    //JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    implementation 'org.testcontainers:testcontainers-bom:1.20.2'
    //SOLAPI - 백엔드 문자 발송 서비스를 위한 문자 서비스 API
    implementation 'com.solapi:sdk:1.0.3'
    //JACSON
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    //테스트 컨테이너
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:testcontainers'
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll '-Xjsr305=strict'
    }
}

allOpen {
    annotation 'jakarta.persistence.Entity'
    annotation 'jakarta.persistence.MappedSuperclass'
    annotation 'jakarta.persistence.Embeddable'
}

tasks.named('test') {
    useJUnitPlatform()
}

/**
 * JaCoCo Code Coverage 설정
 */
jacoco {
    toolVersion = "0.8.13"
    reportsDirectory = layout.buildDirectory.dir("reports/jacoco").get().asFile
}
jacocoTestReport {

    reports {
        html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
        xml.required = false
        csv.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(
                classDirectories.files.collect { file ->
                    fileTree(dir: file, exclude: ['**/*Application*',
                                                  '**/*Config*',
                                                  '**/dto/**',
                                                  '**/vo/**',
                                                  '**/domain/**',
                                                  '**/*Exception*',
                                                  '**/*Properties*',
                                                  '**/*Mapper*',
                                                  '**/*ErrorCode*',
                                                  '**/*ErrorResponse*']
                    )
                })
    }

    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true

            element = 'CLASS' // 클래스 단위로 검증

            // 브랜치 커버리지를 최소 90% 로 설정
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.90
            }

            //라인 커버리지를 최소 90%로 설정
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = BigDecimal.valueOf(0.9) // 90% coverage 로 설정
            }

            //빈 줄을 제외한 코드의 라인 수를 최대 150으로 제한
            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                maximum = BigDecimal.valueOf(150)
            }
        }
    }

    afterEvaluate {
        classDirectories.setFrom(
                classDirectories.files.collect { file ->
                    fileTree(dir: file, exclude: ['**/*Application*',
                                                  '**/*Config*',
                                                  '**/*Dto*',
                                                  '**/vo/**',
                                                  '**/*Entity*',
                                                  '**/*Exception*',
                                                  '**/*Properties*',
                                                  '**/*Mapper*',
                                                  '**/*ErrorCode*',
                                                  '**/*ErrorResponse*']
                    )
                })
    }

    dependsOn test
}

tasks.named('test') {
    useJUnitPlatform()

    jacoco {
        destinationFile = file("$buildDir/jacoco/jacoco.exec")
    }
    finalizedBy jacocoTestReport // report is always generated after tests
}
